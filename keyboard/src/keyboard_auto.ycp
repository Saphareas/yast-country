/**
 * Autoinstallation client for keyboard setting
 * Author	: Jiri Suchomel <jsuchome@suse.cz>
 * $Id$
 */
{
    /* This is a special case of auto client; currently doesn't follow usual API
       but onlyu provides the "Change" function of other auto-clients

       Might become full-featured auto-client if necessary
    */

    import "Arch";
    import "AutoinstGeneral";
    import "GetInstArgs";
    import "Keyboard";
    import "Mode";
    import "Wizard";

    include "keyboard/dialogs.ycp";

    /**
     * Autoinstallation config sequence
     */
    define symbol AutoSequence () {

	// initialization stuff
	if (AutoinstGeneral::keyboard["keymap"]:"" != "")
	{
	    Keyboard::current_kbd = AutoinstGeneral::keyboard["keymap"]:"";
	    if (AutoinstGeneral::keyboard["keyboard_values"]:$[] != $[])
	    {
		Keyboard::SetExpertValues (
		    AutoinstGeneral::keyboard["keyboard_values"]:$[]);
	    }
	}
	else
	{
	    Keyboard::current_kbd	= Keyboard::GetKeyboardForLanguage (
		AutoinstGeneral::language, "english-us");
	}
	Keyboard::Read (); // save the inital values

	Wizard::CreateDialog();
	Wizard::HideAbortButton();

	symbol result = KeyboardDialog (GetInstArgs::argmap());

	Wizard::CloseDialog ();

	AutoinstGeneral::keyboard["keymap"]  = Keyboard::current_kbd;

	return result;
    }

    if (Mode::config () && !Arch::s390 ())
	return AutoSequence ();
    else
	return `next;
}
